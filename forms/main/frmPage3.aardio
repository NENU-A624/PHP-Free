import win.ui;
import fonts.fontAwesome;
/*DSG{{*/
var winform = win.form(text="NATAPP";right=759;bottom=469;border="thin";clipch=1;exmode="none";max=false;min=false;mode="popup";topmost=1)
winform.add(
ClearButton={cls="button";text="Clear";left=320;top=422;right=420;bottom=452;db=1;dl=1;dr=1;z=1};
DebugButton={cls="plus";left=20;top=10;right=60;bottom=50;background="\res\NATAPP\简单模式.png";dl=1;dt=1;notify=1;z=6};
Info={cls="plus";left=20;top=436;right=40;bottom=456;color=8388608;db=1;dl=1;iconColor=15780518;iconStyle={font=LOGFONT(h=-20;name='FontAwesome')};iconText='\uF05A';notify=1;z=2};
StartButton={cls="plus";left=698;top=10;right=738;bottom=50;align="left";background="\res\NATAPP\开关关闭.png";dr=1;dt=1;font=LOGFONT(h=-16);iconStyle={font=LOGFONT(h=-20;name='FontAwesome')};notify=1;z=4};
Web={cls="plus";left=732;top=444;right=752;bottom=464;background="\res\NATAPP\开关关闭-按下.png";color=8388608;db=1;dr=1;foreRepeat="scale";iconColor=15780518;notify=1;z=7};
edit={cls="richedit";left=20;top=62;right=738;bottom=414;autohscroll=false;autovscroll=false;db=1;dl=1;dr=1;dt=1;edge=1;hscroll=1;link=1;multiline=1;vscroll=1;z=3};
editAuthtoken={cls="plus";left=70;top=20;right=698;bottom=40;bgcolor=15724527;border={color=-16777216};dl=1;dr=1;dt=1;editable="edit";font=LOGFONT(h=-16);textPadding={right=4};z=5}
)
/*}}*/


import win.ui.atom;
var atom,hwnd = winform.atom("{D053A1BA-AB22-4757-8B26-964C9054C847}");
if(!atom){
	win.showForeground(hwnd);
	win.quitMessage();	
	return;
}

import fsys.config;
var config = fsys.config(io.appData("aardio/natapp/config"));
winform.bindConfig( config.winform,{ 
	plus ={ 
		["^edit"] = "text";
	};  
} );

winform.DebugButton.skin({
	background = { 
		hover = "\res\NATAPP\简单模式-按下.png"; 
	};
})
winform.StartButton.skin({
	background = { 
		hover = "\res\NATAPP\开关关闭-按下.png"; 
	};
})

winform.editAuthtoken.setCueBannerText("请输入   Authtoken ");
import win.util.tray;
var tray;
if(winform.editAuthtoken.text == null||winform.editAuthtoken.text == ""){
	tray = win.util.tray(winform,"\res\NATAPP\TrayIcon.ico","NATAPP:请输入 Authtoken");
}else {
	tray = win.util.tray(winform,"\res\NATAPP\TrayIcon.ico","隧道未开启");
}

import win.dlg.message;
var msgDlg = win.dlg.message(winform); 



winform.wndproc = {
	[0xACCF/*_WM_TRAYMESSAGE*/ ] = function(hwnd,message,wParam,lParam){
		if( lParam = 0x205/*_WM_RBUTTONUP*/ ){ 
			var pt = ::POINT();
			::User32.GetCursorPos(pt);
			
			win.setForeground(winform.hwnd)
			winform.popmenu.popup(pt.x,pt.y,true )
		}	
		elseif( lParam = 0x203/*_WM_LBUTTONDBLCLK*/ ){
			winform.show(true);
		} 
	}
}
winform.popmenu = win.ui.popmenu(winform);
/*
winform.popmenu.add('显示窗口',function(id){
	winform.show(true);
})
*/
winform.popmenu.add('查看隧道',function(id){
	import process;
	process.openUrl("https://natapp.cn/tunnel/lists");
})
/*
winform.popmenu.add('退出程序',function(id){ 
	winform.onClose = null;
	winform.close(); 
})
*/
var debugMode = false;
winform.DebugButton.oncommand = function(id,event){
	if(!debugMode){
		winform.DebugButton.background = "\res\NATAPP\调试模式.png";
		winform.DebugButton.skin({
			background = { 
				default = "\res\NATAPP\调试模式.png";
				hover = "\res\NATAPP\简单模式-按下.png"; 
			};
		})
		winform.edit.print( "日志等级变更为[DEBUG],请重启服务！" );
		var form = msgDlg.warn('Debug Mode ON !',2000);
		debugMode = true;
	}else {
		winform.DebugButton.background = "\res\NATAPP\简单模式.png";
		winform.DebugButton.skin({
			background = { 
				default = "\res\NATAPP\简单模式.png";
				hover = "\res\NATAPP\简单模式-按下.png"; 
			};
		})
		winform.edit.print( "日志等级变更为[INFO],请重启服务！" );
		var form = msgDlg.warn('Debug Mode OFF !',2000);
		debugMode = false;
	}
	
}
var start = false;
import process.natapp;
winform.StartButton.oncommand = function(id,event){
	if(!start){		
		var token = string.trim(winform.editAuthtoken.text)
		if(!#token){
			return winform.editAuthtoken.editBox.showErrorTip("错误","请输入 Authtoken")
		}					
		process.natapp.logger = winform.edit;
		natapp  = process.natapp(token,winform.edit,debugMode?"DEBUG":"INFO");
		if(natapp.tunnelUrl){
			tray.tip = "当前穿透网址: " + natapp.tunnelUrl;
			winform.edit.print( "当前穿透网址: " + natapp.tunnelUrl );
			if(natapp.tunnelInfo){
				winform.edit.print(natapp.tunnelInfo)
			}
			winform.StartButton.background = "\res\NATAPP\开关打开.png";
			winform.StartButton.skin({
				background = { 
					default = "\res\NATAPP\开关打开.png";
					hover = "\res\NATAPP\开关关闭-按下.png"; 
				};
			})
			start = true;
		}
		else {
			tray.tip = "NATAPP:认证失败";
			winform.edit.print( "认证失败" );	
		}
	}else {
		tray.tip = "隧道未开启";
		winform.StartButton.background = "\res\NATAPP\开关关闭.png";
		winform.StartButton.skin({
			background = { 
				default = "\res\NATAPP\开关关闭.png";
				hover = "\res\NATAPP\开关关闭-按下.png"; 
			};
		})
		if(natapp){
			natapp.ctrlEvent(0);
			natapp.close();
		}
		winform.edit.print( "隧道已关闭！" );
		start = false;
	}
	
}

//清理按钮
winform.ClearButton.oncommand = function(id,event){
	winform.edit.text = "";
}
winform.onMinimize = function(lParam){
	winform.show(false); 
	return true;
}
winform.onClose = function(hwnd,message,wParam,lParam){
	winform.show(false);
	return true;    
}



winform.Info.skin({
	iconColor={
		default=0xFFFFFFFF;
		hover = 0xFF00FF00
	}
})

winform.Info.oncommand = function(id,event){
	var form = msgDlg.create('作者:CloudLake',,true);
	for(i=1;100;1){
		if(!form.valid){ break; }
		form.progress.progressPos = form.progress.progressPos + 1;
		win.delay(50); 
	}
	form.close();
}

winform.edit.onlink=function(message,href){
	if( message = 0x202/*_WM_LBUTTONUP*/ ) {
		import process;
		process.openUrl(href);
	}
}


//两句代码创建玻璃窗体
//import win.ui.glass;
//win.ui.glass( winform);

winform.Web.skin(
	background = { 
		hover = "\res\NATAPP\JoinColor.ico";
	}
)
var ty = time(,"%Y");
var tm = time(,"%m");
var td = time(,"%d");

winform.Web.oncommand = function(id,event){
	var form;
	if((tonumber(tostring(ty)++tostring(tm)++tostring(td))>=20220201)&&(tonumber(tostring(ty)++tostring(tm)++tostring(td))<=20220215)){
		form = msgDlg.create('祝Join工作室全体成员2022新春快乐！',,true);	
	}else {
		form = msgDlg.create('Join工作室内部专用',,true);
	}
	for(i=1;100;1){
		if(!form.valid){ break; }
		form.progress.progressPos = form.progress.progressPos + 1;
		win.delay(50); 
	}
	form.close();
}

winform.show(); //显示窗口
win.loopMessage(); //启动消息循环